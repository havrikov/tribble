// tribble grammar for tribble grammars
// TODO allow annotating max depth constraints, MinLength, MaxLength
// TODO add comments and whitespace

Grammar = Production+ ;
Production = NonTerminal "=" Alternation ";" ;
Alternation = Concatenation ( "|" Concatenation )* ;
Concatenation = Atom+ ("@@" Probability)? ;
Atom = (
        "(" Alternation ")" |
        Regex |
        Literal |
        NonTerminal ) Quant? ;

Quant = "?" | "+" | "*" | "{," num "}" | "{" num ",}" | "{" num "," num "}" ;

Probability = /[0-9A-Fa-fxXpP.-]+/ ;
num = /0|([1-9][0-9]*)/ ;
NonTerminal = /[A-Za-z0-9_'$]+/ ;
Literal = "\"" ( /[^\"\\\\]/ | "\\" /[nrt\"\\\\]/ )* "\"" ;
Regex = "/" regexp "/";

// copied from regex.tribble

regexp  = unionexp;
unionexp = interexp ( "|" unionexp )?;
interexp = concatexp ( "&" interexp )?;
concatexp = repeatexp concatexp?;

repeatexp = repeatexp "?"
            | repeatexp "*"
            | repeatexp "+"
            | repeatexp "{" num "}"
            | repeatexp "{" num ",}"
            | repeatexp "{" num "," num "}"
            | complexp;

complexp = "~" complexp | charclassexp;

charclassexp = "[" charclasses "]"
               | "[^" charclasses "]"
               | simpleexp;

charclasses = charclass charclasses?;
charclass = charexp ("-" charexp)?;

simpleexp = charexp
            | "." // any single character
            | "\"" (/[^\\\"\)\(]/|"\\\\"|"\\)"|"\\(")+ "\"" // Unicode string without double-quotes
            | "()" // the empty string
            | "(" unionexp ")";

charexp = /[^\"\\\/\(\)]/ // a single non-reserved character
          | "\\" /[\"\\\/\(\)]/; // a single character

